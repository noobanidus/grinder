buildscript {
    repositories {
        mavenLocal()
        maven { url = 'http://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
        maven {
            url 'https://dogforce-games.com/maven'
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.0.165-run-configs-rework', changing: true
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'

version = "${minecraft_version}-${mod_version}"
group = "noobanidus.mods"
archivesBaseName = "grindr"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: "${mcp_mappings}"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            taskName 'runClient'

            workingDirectory project.file('run')

            //property 'forge.logging.markers', 'CORE,REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                grindr {
                    source sourceSets.main
                }
            }
        }

        server {
            taskName 'runServer'

            workingDirectory project.file('run')

            //property 'forge.logging.markers', 'CORE,REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                grindr {
                    source sourceSets.main
                }
            }
        }

        data {
            taskName 'runData'

            workingDirectory project.file('run')

            environment 'target', 'fmluserdevdata'

            //property 'forge.logging.markers', 'CORE,REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                grindr {
                    source sourceSets.main
                }
            }

            args '--mod', 'grindr', '--all', '--output', '"' + rootProject.file('src/generated/resources/') + '"',
                    '--existing', '"' + rootProject.file('src/main/resources') + '"'//, '--existing', '"' + rootProject.file('src/generated/resources') + '"'

        }
    }
}

configurations {
    shade
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    maven {
        url "https://maven.blamejared.com"
    }
    maven {
        url "http://maven.tterrag.com/"
    }
    maven { // JEI
        url "http://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compile fg.deobf("com.blamejared.crafttweaker:CraftTweaker-${minecraft_version}:${crt_version}")

    compile fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}")

    def registrate = "com.tterrag.registrate:Registrate:MC${minecraft_version}-${registrate_version}"
    implementation fg.deobf(registrate)
    shade registrate

    def noobutil = "noobanidus.libs:noobutil:${minecraft_version}-${noobutil_version}"
    implementation fg.deobf(noobutil)
    shade noobutil

    def shoulders = "noobanidus.mods.shoulders:shoulders:${minecraft_version}-${shoulders_version}:embed"
    implementation fg.deobf(shoulders)
    shade shoulders
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate 'com.tterrag.registrate', 'noobanidus.mods.repack_grindr.registrate'
    relocate 'noobanidus.libs.noobutil', 'noobanids.libs.repack_grindr.noobutil'
    relocate 'noobanidus.mods.shoulders', 'noobanidus.mods.repack_grindr.shoulders'
    classifier ''
}

jar.classifier = 'nodep'

reobf {
    shadowJar {}
}

build.dependsOn reobfShadowJar


